/*
 * Copyright (c) 2014 Jacob D. Parr
 *
 * This software may not be used without permission.
 */
project(":tioga-jobs-agent") {

  version = "1.0.1"

  dependencies {
    compile(project(":tioga-jobs-pub"))
    compile(project(":tioga-jobs-jackson"))

    compile("org.tiogasolutions.dev:tioga-dev-jackson:+")
    compile("org.tiogasolutions.lib:tioga-lib-jaxrs-jackson:+")
    compile("org.tiogasolutions.lib:tioga-lib-couchace:+")
    compile("org.tiogasolutions.app:tioga-app-common:+")

    compile("org.tiogasolutions.couchace:tioga-couchace-core:+")
    compile("org.tiogasolutions.couchace:tioga-couchace-annotations:+")
    compile("org.tiogasolutions.couchace:tioga-couchace-jersey:+")
    compile("org.tiogasolutions.couchace:tioga-couchace-jackson:+")

    compile ("org.slf4j:slf4j-simple:1.7.10")
    compile ("org.thymeleaf:thymeleaf:2.1.3.RELEASE")
    compile ("org.thymeleaf.extras:thymeleaf-extras-java8time:2.1.0.RELEASE")
    compile("org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.17")

    testCompile ("org.testng:testng:6.8.21")
  }

  task createStartScript {
    File scripts = file("$buildDir/scripts")
    outputs.dir scripts
    doLast {
      scripts.mkdirs()
      String contents = "@echo off\r\ncls\r\n";
      contents += "echo Starting Tioga Solution's Jobs Agent\r\n";
      contents += "%JAVA_HOME%\\bin\\java.exe -jar ./lib/tioga-jobs-agent-${project.version}.jar";
      new File(scripts, "server-start.bat").write(contents)
    }
  }

  apply plugin:'distribution'

  artifacts {
    archives distZip
  }

  distributions {
    main {
      contents {
        into("lib") {
          from(jar)
          from(project.configurations.runtime)
        }
        from(createStartScript)
      }
    }
  }

  jar {
    manifest {
      attributes(
        "Implementation-Version" : "0.1",
        "Main-Class"             : "org.tiogasolutions.jobs.agent.grizzly.GrizzlyJobsAgent",
        "Class-Path"             : configurations.compile.collect { it.getName() }.join(' ')
      )
    }
  }

  apply(from: urlCache.get("https://raw.githubusercontent.com/tioga/build-scripts/master/build-config-deployer.gradle"))
}
